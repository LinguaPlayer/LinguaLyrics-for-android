apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "habibkazemi.ir.lingualyrics_android"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode generateVersionCode() // 150010203
        versionName generateVersionName() // 1.2.3-SNAPSHOT
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

private Integer generateVersionCode() {
    return rootProject.minSdkVersion * 10000000 + rootProject.versionMajor * 10000 + rootProject.versionMinor * 100 + rootProject.versionPatch
}

private String generateVersionName() {
    String versionName = "${rootProject.versionMajor}.${rootProject.versionMinor}.${rootProject.versionPatch}"
    if (rootProject.versionClassifier == null) {
        if (rootProject.isSnapshot) {
            rootProject.versionClassifier = "SNAPSHOT"
        }
    }

    if (rootProject.versionClassifier != null) {
        versionName += "-" + rootProject.versionClassifier
    }
    return versionName
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraintLayoutVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-vector-drawable:$rootProject.supportLibraryVersion"
    implementation "com.android.support:palette-v7:$rootProject.supportLibraryVersion"
    implementation "com.jakewharton:butterknife:$rootProject.butterknifeVersion"
    implementation "com.android.support:preference-v7:$rootProject.supportLibraryVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterknifeCompilerVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // material search view
    implementation "com.miguelcatalan:materialsearchview:$rootProject.materialsearchview"

    // okhttp
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttp"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit"

    // LoadingIndicator
    implementation "com.wang.avi:library:$rootProject.loadingIndicator"

    // Picasso
    implementation "com.squareup.picasso:picasso:$rootProject.picasso"

    // parceler
    implementation "org.parceler:parceler-api:$rootProject.parceler"
    annotationProcessor "org.parceler:parceler:$rootProject.parceler"

    // Test dependencies
    testImplementation "junit:junit:$rootProject.jUnitVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.testVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espressoCoreVersion"

    // Room
    implementation "android.arch.persistence.room:runtime:$rootProject.room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$rootProject.room_version"
    testImplementation "android.arch.persistence.room:testing:$rootProject.room_version"

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$rootProject.lifecycle_version"

    // Navigation
    implementation "android.arch.navigation:navigation-fragment:$rootProject.nav_version"
    implementation "android.arch.navigation:navigation-ui:$rootProject.nav_version"

    // Paging
    implementation "android.arch.paging:runtime:$rootProject.paging_version"

    // Timber
    implementation "com.jakewharton.timber:timber:$rootProject.timber"


}
repositories {
    mavenCentral()
}
